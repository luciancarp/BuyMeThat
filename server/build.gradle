buildscript {
    ext {
        springBootVersion = '2.0.0.M6'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.0.3"
        classpath 'org.postgresql:postgresql:42.1.4'
        classpath 'com.google.api-client:google-api-client:1.23.0'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
    id 'nu.studer.jooq' version '2.0.9'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'nu.studer.jooq'
apply plugin: 'io.spring.dependency-management'


group = 'com.buymethat'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile('org.jooq:jooq')
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jooq')
    compile('org.postgresql:postgresql:42.1.4')
    compile('com.google.api-client:google-api-client:1.23.0')

    jooqRuntime 'org.postgresql:postgresql:42.1.4'
    runtime('org.springframework.boot:spring-boot-devtools')

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

flyway {
    cleanOnValidationError = true
    url = 'jdbc:postgresql://localhost:5432/buymethatdb'
    user = 'buymethatapi'
    password = 'housecatwindowcake'
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    MainDB(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:5432/buymethatdb'
            user = 'buymethatapi'
            password = 'housecatwindowcake'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'com.buymethat.db'
            }
        }
    }
}

generateMainDBJooqSchemaSource.dependsOn flywayMigrate
test.dependsOn flywayClean
